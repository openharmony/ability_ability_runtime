# Copyright (c) 2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/ability/ability_runtime/ability_runtime.gni")


config("ability_config") {
  visibility = [ ":*" ]
  visibility += [
    "${ability_runtime_native_path}/ability/native/*",
    "${ability_runtime_test_path}/*",
    "${distributeddatamgr_path}/data_share/interfaces/inner_api/*",
    "${imf_path}/frameworks/kits/extension/*",
    "${print_fwk_path}/frameworks/kits/extension/*",
    "${user_file_service_innerkits_path}/file_access/*",
    "${wallpaper_mgr_path}/frameworks/kits/extension/*",
  ]
  include_dirs = [
    "${ability_runtime_path}/interfaces/kits/native/ability/native",
    "${ability_runtime_path}/interfaces/kits/native/ability/native/continuation/distributed",
    "${ability_runtime_path}/interfaces/kits/native/ability/native/continuation/kits",
    "${ability_runtime_path}/interfaces/kits/native/ability/native/continuation/remote_register_service",
    "${ability_runtime_path}/interfaces/kits/native/ability/native/distributed_ability_runtime",
    "${ability_runtime_path}/interfaces/kits/native/appkit/app",
    "${ability_runtime_innerkits_path}/app_manager/include/appmgr",
    "${ability_runtime_innerkits_path}/uri/include",
    "${ability_runtime_services_path}/abilitymgr/include/utils",
    "${ability_runtime_services_path}/abilitymgr/include",
    "${ability_runtime_path}/interfaces/kits/native/appkit/ability_runtime/app",
    "${ability_runtime_innerkits_path}/ability_manager/include/continuation",
    "${ability_runtime_path}/interfaces/kits/native/appkit/app/task",
    "${ability_runtime_napi_path}/inner/napi_common",
    "${ability_runtime_napi_path}/featureAbility",
  ]

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
  defines = [ "AMS_LOG_TAG = \"Ability\"" ]
  defines += [ "AMS_LOG_DOMAIN = 0xD001300" ]
  if (target_cpu == "arm64") {
    defines += [ "_ARM64_" ]
  }

  if (target_cpu == "arm64") {
    defines += [ "APP_USE_ARM64" ]
  } else if (target_cpu == "arm") {
    defines += [ "APP_USE_ARM" ]
  } else if (target_cpu == "x86_64") {
    defines += [ "APP_USE_X86_64" ]
  }

  if (ability_runtime_graphics) {
    defines += [
      "SUPPORT_GRAPHICS",
      "SUPPORT_SCREEN",
    ]
  }
}

config("agent_ui_extension_config") {
  visibility = [ ":*" ]
  include_dirs = [
      "${ability_runtime_path}/interfaces/kits/native/ability/native/ability_runtime",
      "${ability_runtime_path}/interfaces/kits/native/ability/native/ui_extension_base",
      "${ability_runtime_path}/interfaces/kits/native/ability/ability_runtime",
      "${ability_runtime_path}/interfaces/kits/native/ability/native",
      "${ability_runtime_path}/interfaces/kits/native/appkit/ability_runtime",
      "${ability_runtime_path}/interfaces/kits/native/appkit/ability_runtime/context",
  ]
}

ohos_shared_library("agent_ui_extension") {
  sanitize = {
      integer_overflow = true
      ubsan = true
      boundary_sanitize = true
      cfi = true
      cfi_cross_dso = true
      cfi_vcall_icall_only = true
      debug = false
  }
  branch_protector_ret = "pac_ret"

  configs = [
      ":agent_ui_extension_config",
      "${ability_runtime_services_path}/common:optimize_config",
  ]

  include_dirs = [ "${agent_runtime_framework_path}/interfaces/kits/native/agent_ui_extension", ]

  sources = [
      "${agent_runtime_framework_path}/frameworks/native/agent_ui_extension_ability/agent_ui_extension.cpp",
      "${agent_runtime_framework_path}/frameworks/native/agent_ui_extension_ability/js_agent_ui_extension.cpp",
  ]

  deps = [
      "${ability_runtime_native_path}/ability/native:abilitykit_native",
      "${ability_runtime_native_path}/ability/native:abilitykit_utils",
      "${ability_runtime_native_path}/ability/native:extensionkit_native",
      "${ability_runtime_native_path}/ability/native:ui_extension",
      "${ability_runtime_innerkits_path}/ability_manager:ability_manager",
      "${ability_runtime_innerkits_path}/ability_manager:ability_start_options",
      "${ability_runtime_innerkits_path}/runtime:runtime",
      "${ability_runtime_napi_path}/inner/napi_common:napi_common",
      "${ability_runtime_native_path}/ability:ability_context_native",
      "${ability_runtime_native_path}/ability/native:ability_business_error",
      "${ability_runtime_native_path}/appkit:app_context",
  ]

  external_deps = [
      "ability_base:want",
      "c_utils:utils",
      "eventhandler:libeventhandler",
      "hilog:libhilog",
      "hitrace:hitrace_meter",
      "ipc:ipc_napi",
      "ipc:ipc_single",
      "napi:ace_napi",
  ]


  if (ability_runtime_graphics) {
      external_deps += [ "window_manager:libwm" ]
  }

  subsystem_name = "ability"
  part_name = "ability_runtime"
}


ohos_shared_library("agent_ui_extension_module") {
  sanitize = {
      integer_overflow = true
      ubsan = true
      boundary_sanitize = true
      cfi = true
      cfi_cross_dso = true
      cfi_vcall_icall_only = true
      debug = false
  }
  branch_protector_ret = "pac_ret"

  include_dirs = [
      "${agent_runtime_framework_path}/interfaces/kits/native/agent_ui_extension",
  ]
  sources = [ "${agent_runtime_framework_path}/frameworks/native/agent_ui_extension_ability/agent_ui_extension_module_loader.cpp" ]

  configs = [
      ":ability_config",
      ":agent_ui_extension_config",
  ]

  deps = [
      ":agent_ui_extension",
      "${ability_runtime_innerkits_path}/runtime:runtime",
  ]

  external_deps = [
      "ability_base:configuration",
      "ability_base:session_info",
      "ability_base:want",
      "bundle_framework:appexecfwk_core",
      "hilog:libhilog",
      "ipc:ipc_napi",
      "ipc:ipc_single",
      "napi:ace_napi",
  ]

  if (ability_runtime_graphics) {
      external_deps += [
      "form_fwk:form_manager",
      "window_manager:libwm",
      ]
  }

  relative_install_dir = "extensionability/"
  subsystem_name = "ability"
  part_name = "ability_runtime"
}