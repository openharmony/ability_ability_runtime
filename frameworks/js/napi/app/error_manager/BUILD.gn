# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/ability/ability_runtime/ability_runtime.gni")

template("errormanager") {
  ohos_shared_library(target_name) {
    forward_variables_from(invoker, [ "relative_install_dir" ])

    if (defined(invoker.defines)) {
      defines = invoker.defines
    }

    include_dirs = []

    sources = [
      "error_manager_module.cpp",
      "js_error_manager.cpp",
      "js_error_observer.cpp",
    ]

    configs = [ "${ability_runtime_services_path}/common:common_config" ]

    deps = [
      "${ability_runtime_innerkits_path}/ability_manager:ability_manager",
      "${ability_runtime_innerkits_path}/ability_manager:ability_start_options",
      "${ability_runtime_innerkits_path}/app_manager:app_manager",
      "${ability_runtime_innerkits_path}/runtime:runtime",
      "${ability_runtime_native_path}/ability/native:uiabilitykit_native",
      "${ability_runtime_native_path}/appkit:appkit_native",
    ]

    external_deps = [
      "c_utils:utils",
      "eventhandler:libeventhandler",
      "hilog:libhilog",
      "napi:ace_napi",
    ]

    subsystem_name = "ability"
    part_name = "ability_runtime"
  }
}

errormanager("errormanager_napi") {
  relative_install_dir = "module/application"
}

errormanager("errormanager") {
  relative_install_dir = "module/app/ability"
  defines = [ "ENABLE_ERRCODE" ]
}
