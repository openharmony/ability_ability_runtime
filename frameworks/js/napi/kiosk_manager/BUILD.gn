# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/ability/ability_runtime/ability_runtime.gni")

template("kioskmanager") {
  ohos_shared_library(target_name) {
    sanitize = {
      cfi = true
      cfi_cross_dso = true
      debug = false
    }
    forward_variables_from(invoker, [ "relative_install_dir" ])

    defines = []
    if (defined(invoker.defines)) {
      defines += invoker.defines
    }

    sources = [
      "kiosk_manager_module.cpp",
      "js_kiosk_manager.cpp",
    ]

    include_dirs = [ "./" ]

    deps = [
      "${ability_runtime_innerkits_path}/ability_manager:ability_manager",
      "${ability_runtime_innerkits_path}/napi_base_context:napi_base_context",
      "${ability_runtime_innerkits_path}/runtime:runtime",
      "${ability_runtime_napi_path}/inner/napi_common:napi_common",
      "${ability_runtime_native_path}/ability:ability_context_native",
    ]

    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
      "napi:ace_napi",
    ]

    subsystem_name = "ability"
    part_name = "ability_runtime"
  }
}

kioskmanager("kioskmanager_napi") {
  relative_install_dir = "module/application"
}

kioskmanager("kioskmanager") {
  relative_install_dir = "module/app/ability"
  defines = [ "ENABLE_ERRCODE" ]
}
