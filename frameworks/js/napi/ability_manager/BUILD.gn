# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/ability/ability_runtime/ability_runtime.gni")

template("abilitymanager") {
  ohos_shared_library(target_name) {
    forward_variables_from(invoker, [ "relative_install_dir" ])

    if (defined(invoker.defines)) {
      defines = invoker.defines
    }

    sources = [
      "ability_manager_module.cpp",
      "js_ability_foreground_state_observer.cpp",
      "js_ability_manager.cpp",
      "js_ability_manager_utils.cpp",
    ]

     include_dirs = [
      "./",
      "//third_party/icu/icu4c/source/common",
    ]

    deps = [
      "${ability_runtime_innerkits_path}/ability_manager:ability_manager",
      "${ability_runtime_innerkits_path}/ability_manager:ability_start_options",
      "${ability_runtime_innerkits_path}/app_manager:app_manager",
      "${ability_runtime_innerkits_path}/napi_base_context:napi_base_context",
      "${ability_runtime_innerkits_path}/runtime:runtime",
      "${ability_runtime_napi_path}/inner/napi_common:napi_common",
      "${ability_runtime_native_path}/ability:ability_context_native",
      "${ability_runtime_native_path}/ability/native:ability_business_error",
      "${ability_runtime_native_path}/ability/native:abilitykit_native",
    ]

    external_deps = [
      "ability_base:base",
      "ability_base:configuration",
      "ability_base:want",
      "access_token:libtokenid_sdk",
      "c_utils:utils",
      "common_event_service:cesfwk_innerkits",
      "eventhandler:libeventhandler",
      "hilog:libhilog",
      "ipc:ipc_core",
      "ipc:ipc_napi",
      "napi:ace_napi",
      "samgr:samgr_proxy",
    ]

    subsystem_name = "ability"
    part_name = "ability_runtime"
  }
}

abilitymanager("abilitymanager_napi") {
  relative_install_dir = "module/application"
}

abilitymanager("abilitymanager") {
  relative_install_dir = "module/app/ability"
  defines = [ "ENABLE_ERRCODE" ]
}
