# Copyright (c) 2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/ability/ability_runtime/ability_runtime.gni")

config("ability_manager_public_config") {
  visibility = [ ":*" ]
  visibility += [
    "${ability_runtime_test_path}/fuzztest/*",
    "${ability_runtime_test_path}/moduletest/*",
    "${ability_runtime_test_path}/unittest/*",
    "${form_fwk_path}/test/fuzztest/*",
    "${os_account_path}/tools/test/moduletest/acm/*",
  ]
  include_dirs = [
    "include/",
    "include/status_bar_delegate",
    "${ability_runtime_path}/interfaces/kits/native/ability/native",
    "${ability_runtime_path}/interfaces/kits/native/ability/native/continuation/kits",
    "${ability_runtime_path}/interfaces/kits/native/ability/native/continuation/distributed",
    "${ability_runtime_path}/interfaces/kits/native/ability/ability_runtime",
    "${ability_runtime_path}/interfaces/kits/native/appkit/ability_runtime/context",
    "${bundlefwk_inner_api_path}/appexecfwk_base/include",
    "${ability_runtime_innerkits_path}/ability_manager/include",
    "${ability_runtime_innerkits_path}/app_manager/include/appmgr",
    "${ability_runtime_path}/interfaces/kits/native/appkit/app",
    "${ability_runtime_path}/interfaces/kits/native/appkit/dfr",
    "${ability_runtime_path}/interfaces/kits/native/appkit",
    "${ability_runtime_innerkits_path}/dataobs_manager/include",
  ]

  defines = []

  if (ability_command_for_test) {
    defines += [ "ABILITY_COMMAND_FOR_TEST" ]
  }

  if (ability_runtime_graphics) {
    include_dirs += [ "${multimedia_path}/interfaces/innerkits/include" ]
    defines += [ "SUPPORT_GRAPHICS" ]
  }

  cflags = []
  if (target_cpu == "arm") {
    cflags += [ "-DBINDER_IPC_32BIT" ]
  }
}

config("ability_manager_exception_config") {
  cflags_cc = [ "-fexceptions" ]
}

ohos_shared_library("ability_manager") {
  branch_protector_ret = "pac_ret"

  sources = [
    "${ability_runtime_native_path}/ability/native/data_ability_operation.cpp",
    "${ability_runtime_native_path}/ability/native/data_ability_operation_builder.cpp",
    "${ability_runtime_native_path}/ability/native/data_ability_result.cpp",
    "${ability_runtime_native_path}/ability/native/task_handler.cpp",
    "${ability_runtime_native_path}/ability/native/task_handler_client.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/ability_auto_startup_client.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/ability_manager_client.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/ability_manager_proxy.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/ability_running_info.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/ability_scheduler_stub.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/acquire_share_data_callback_proxy.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/acquire_share_data_callback_stub.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/auto_startup_info.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/caller_info.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/dialog_session/dialog_session_info.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/exit_reason.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/extension_running_info.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/insight_intent_execute_callback_proxy.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/insight_intent_execute_callback_stub.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/insight_intent_execute_param.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/insight_intent_execute_result.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/launch_param.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/lifecycle_state_info.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/mission_listener_stub.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/open_link/open_link_options.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/prepare_terminate_callback_proxy.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/prepare_terminate_callback_stub.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/remote_mission_listener_stub.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/remote_on_listener_proxy.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/remote_on_listener_stub.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/system_ability_token_callback_stub.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/ui_extension_ability_connect_info.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/ui_extension_host_info.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/ui_extension_session_info.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/user_callback_proxy.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/user_callback_stub.cpp",
    "src/status_bar_delegate/status_bar_delegate_proxy.cpp",
    "src/status_bar_delegate/status_bar_delegate_stub.cpp",
    "src/window_manager_service_handler_proxy.cpp",
    "src/window_manager_service_handler_stub.cpp",
  ]

  if (ability_runtime_graphics) {
    sources += [
      "src/ability_first_frame_state_data.cpp",
      "src/ability_first_frame_state_observer_proxy.cpp",
      "src/ability_first_frame_state_observer_stub.cpp",
    ]
  }

  configs = [ ":ability_manager_exception_config" ]

  public_configs = [
    ":ability_manager_public_config",
    "${ability_runtime_services_path}/abilitymgr:abilityms_config",
  ]

  public_deps = [ ":ability_connect_callback_stub" ]

  deps = [
    ":ability_start_options",
    ":ability_start_setting",
    ":mission_info",
    ":process_options",
    "${ability_runtime_innerkits_path}/app_manager:app_manager",
    "${ability_runtime_services_path}/abilitymgr:wantagent_manager",
  ]

  external_deps = [
    "ability_base:base",
    "ability_base:session_info",
    "bundle_framework:libappexecfwk_common",
    "c_utils:utils",
    "common_event_service:cesfwk_innerkits",
    "eventhandler:libeventhandler",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "hitrace:hitrace_meter",
    "ipc:ipc_core",
    "relational_store:native_dataability",
    "samgr:samgr_proxy",
  ]
  if (host_cpu != "arm64") {
    external_deps += [ "relational_store:native_rdb" ]
  }

  public_external_deps = [
    "ability_base:configuration",
    "ability_base:want",
    "ability_base:zuri",
    "background_task_mgr:bgtaskmgr_innerkits",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "init:libbegetutil",
    "jsoncpp:jsoncpp",
    "relational_store:native_dataability",
    "relational_store:native_rdb",
  ]

  if (ability_runtime_graphics) {
    deps += []
    external_deps += [
      "ability_base:session_info",
      "image_framework:image_native",
      "window_manager:libwsutils",
      "window_manager:session_manager_lite",
    ]
    public_external_deps += [ "graphic_2d:color_manager" ]
  }

  cflags_cc = []
  if (os_dlp_part_enabled) {
    cflags_cc += [ "-DWITH_DLP" ]
    external_deps += [ "dlp_permission_service:libdlpparse" ]
  }

  innerapi_tags = [
    "platformsdk",
    "sasdk",
  ]
  subsystem_name = "ability"
  part_name = "ability_runtime"
}

ohos_shared_library("ability_start_options") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    cfi_vcall_icall_only = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  include_dirs = [
    "include/",
    "${ability_runtime_services_path}/common/include",
  ]

  sources =
      [ "${ability_runtime_services_path}/abilitymgr/src/start_options.cpp" ]

  deps = [ ":process_options" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "hisysevent:libhisysevent",
    "ipc:ipc_core",
  ]

  cflags_cc = []
  innerapi_tags = [
    "platformsdk",
    "sasdk",
  ]
  subsystem_name = "ability"
  part_name = "ability_runtime"
}

ohos_shared_library("mission_info") {
  branch_protector_ret = "pac_ret"

  sources = [
    "${ability_runtime_services_path}/abilitymgr/src/mission_info.cpp",
    "${ability_runtime_services_path}/abilitymgr/src/mission_snapshot.cpp",
  ]

  public_configs = [ ":ability_manager_public_config" ]

  external_deps = [
    "c_utils:utils",
    "ipc:ipc_core",
  ]
  public_external_deps = [
    "ability_base:configuration",
    "ability_base:want",
    "ability_base:zuri",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "jsoncpp:jsoncpp",
  ]
  if (ability_runtime_graphics) {
    external_deps += [ "image_framework:image_native" ]
    public_external_deps += [ "graphic_2d:color_manager" ]
  }

  cflags_cc = []
  innerapi_tags = [ "platformsdk" ]
  subsystem_name = "ability"
  part_name = "ability_runtime"
}

ohos_shared_library("ability_manager_c") {
  include_dirs = [ "include" ]
  sources = [ "src/ability_manager_client_c.cpp" ]
  libs = []
  ldflags = [ "-Wl,--exclude-libs=ALL" ]
  deps = [ ":ability_manager" ]
  external_deps = [ "c_utils:utils" ]
  innerapi_tags = [ "platformsdk" ]
  subsystem_name = "ability"
  part_name = "ability_runtime"
}

ohos_shared_library("ability_start_setting") {
  branch_protector_ret = "pac_ret"

  sources = [
    "${ability_runtime_services_path}/abilitymgr/src/ability_start_setting.cpp",
  ]

  public_configs = [ ":ability_manager_public_config" ]

  external_deps = [ "c_utils:utils" ]
  public_external_deps = [
    "ability_base:configuration",
    "ability_base:want",
    "ability_base:zuri",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "jsoncpp:jsoncpp",
    "napi:ace_napi",
  ]
  if (ability_runtime_graphics) {
    public_external_deps += [ "graphic_2d:color_manager" ]
  }
  cflags_cc = []
  innerapi_tags = [ "platformsdk" ]
  subsystem_name = "ability"
  part_name = "ability_runtime"
}

ohos_shared_library("process_options") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    cfi_vcall_icall_only = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  include_dirs = [
    "include/",
    "${ability_runtime_services_path}/common/include",
  ]

  sources =
      [ "${ability_runtime_services_path}/abilitymgr/src/process_options.cpp" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
  ]

  cflags_cc = []
  innerapi_tags = [ "platformsdk" ]
  subsystem_name = "ability"
  part_name = "ability_runtime"
}

ohos_shared_library("ability_connect_callback_stub") {
  branch_protector_ret = "pac_ret"

  defines = [ "AMS_LOG_TAG = \"Ability\"" ]
  defines += [ "AMS_LOG_DOMAIN = 0xD001300" ]
  include_dirs = [
    "include/",
    "${ability_base_kits_path}/want/include",
    "${ability_runtime_services_path}/abilitymgr/include",
    "${ability_runtime_services_path}/common/include",
  ]

  sources = [ "${ability_runtime_services_path}/abilitymgr/src/ability_connect_callback_stub.cpp" ]

  public_configs = [ ":ability_manager_public_config" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
  ]
  public_external_deps = [
    "ability_base:configuration",
    "ability_base:want",
    "ability_base:zuri",
    "bundle_framework:appexecfwk_base",
    "bundle_framework:appexecfwk_core",
    "jsoncpp:jsoncpp",
  ]
  if (ability_runtime_graphics) {
    public_external_deps += [ "graphic_2d:color_manager" ]
  }

  cflags_cc = []
  innerapi_tags = [
    "platformsdk",
    "sasdk",
  ]
  subsystem_name = "ability"
  part_name = "ability_runtime"
}
